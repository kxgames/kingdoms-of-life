// Josh Mitnick

include "tut_lib.bhs"

scenario {

  // nations
  labels {
    HUMAN = 1,
    COMPUTER = 2,
  }

  labels {
    FLAG_X           = 27,
    FLAG_Y           = 50,
    FLAG2_X          = 25,
    FLAG2_Y          = 33,
    BONUS_LEGIONS_X  = 35,
    BONUS_LEGIONS_Y  = 28,
    ARMY_CREATE_X    = 35,
    ARMY_CREATE_Y    = 28,
    ATTACK_TO_X      = 37,
    ATTACK_TO_Y      = 30,
  }

  static first_flag_x       = 58;
  static first_flag_y       = 76;
  static second_flag_x      = 38;
  static second_flag_y      = 62;
  static city_flag_x        = 25;
  static city_flag_y        = 33;
  static Boadicea_id        = 1;  // HACK!!  find_unit(HUMAN, "BOUDICEAN");
  static briton_id          = find_city_id($S("Briton"));
  static Briton_X           = object_position_x (HUMAN, briton_id);
  static Briton_Y           = object_position_y (HUMAN, briton_id);
  static Legions_create_X   = Briton_X;
  static Legions_create_Y   = Briton_Y;
  static barracks_id        = find_build(COMPUTER, "barracks");
  static R_Barracks_X       = object_position_x (COMPUTER, barracks_id);
  static R_Barracks_Y       = object_position_y (COMPUTER, barracks_id);
  static lookout_id         = find_build( COMPUTER, "lookout" );
  static lookout_x          = object_position_x( COMPUTER, lookout_id );
  static lookout_y          = object_position_y( COMPUTER, lookout_id );
  static tower_destroyed    = false;
  static barracks_destroyed = false;
  static army_destroyed     = false;
  static objective_delay    = 60;
  static intro_played       = false;

  run_once {
    bubble_text_hide();
    set_explored(HUMAN, ( (R_Barracks_X + lookout_x ) / 2) , ( ( R_Barracks_Y + lookout_y ) / 2 ), 10);
    set_explored(HUMAN, object_position_x( HUMAN, Boadicea_id ), object_position_y( HUMAN, Boadicea_id ), 20 );
    disable_all_triggers();
    //set_unique_unit( "BOUDICEAN", "Boadicea");
    set_object_type_max_health( "barracks", 250 );
    set_object_type_max_health( "lookout", 125 );
    disable_type( "Citizen" );
    set_music_volume(50);
    enable_trigger("Global");
    enable_trigger("player_lost");
    enable_trigger("Start");
    enable_trigger("boadicea_lost");
    set_timer("boadicea_deselect", 1);
    set_timer("progress_check",objective_delay );
  }

  trigger Objective_Reminder( timer_expired("progress_check") && sound_finished() ) {
    clear_all_arrows();
    right = get_objectives_right();
    top   = get_objectives_top();

    if (right >= 0 && top >= 0) {
      draw_screen_arrow_line( right + 75, top - 75, right, top, 10 );
      play_sound("objective.wav");
      if(objective_delay < 300) {
       objective_delay = objective_delay * 2;
      } else {
       objective_delay = 300;
      }

    }
    set_timer("progress_check", objective_delay);
    enable_trigger("Objective_Reminder");
  }

  trigger Start () {
    //WAV: We must remove the Romans from the lands of Briton.  Lead Boudecia back to the city 
  //     where we will muster our troops to drive the Romans out.
    play_sound ("Boadicea.wav");
    enable_trigger("Select_a_unit");
    enable_trigger("Select_Boadicea");
    enable_trigger("Objective_Reminder");
    set_timer("progress_check",objective_delay);
  }

  trigger Select_a_unit( sound_finished() ) {
    intro_played = true;
    //WAV: "You must select a unit before you can give it orders. Move the mouse over Boadicea and left click to 'select' her."
    add_objective($S("Left click on Boadicea."), "Select_Boadicea", "select_Boadicea.wav");    
    play_objective_sound("Select_Boadicea");

    enable_trigger ("Select_Boadicea");
    enable_trigger ("Ping_Boadicea");
    set_timer("progress_check",objective_delay);
  }

  trigger Select_Boadicea( object_selected(HUMAN, Boadicea_id ) && intro_played ) {
    disable_trigger ("Ping_Boadicea");
    disable_trigger("Select_a_unit");
    objective_complete("Select_Boadicea");

    add_flag( first_flag_x, first_flag_y );

    // WAV: "Well done. You can be sure you have the right unit by looking at the name of 
  // the unit you've selected in the lower right panel. Now you can give Boadicea orders."
    play_sound("Boadicea_selected.wav");

    // draw arrow to unit display info
    unit_display_left = get_button_left("right_window");
    unit_display_top  = get_button_top("right_window");
    draw_screen_arrow_line( unit_display_left - 100, unit_display_top - 100, unit_display_left, unit_display_top, 0 );

    enable_trigger ("talk_about_actions");
    enable_trigger("moved_to_flag");
    enable_trigger ("Deselect_Boadicea");
    set_timer("progress_check",objective_delay);
  }

  trigger Deselect_Boadicea( !object_selected(HUMAN, Boadicea_id ) && sound_finished() && timer_expired("boadicea_deselect")) {
    play_sound ("lostboud.wav");
    //WAV: Left-clicking on the map will clear all selections. To regain control of Boadicea, left click on her, then
  //     right click where you want her to go.***/
    set_timer("progress_check",objective_delay);
    enable_trigger("deselect_boadicea");
    set_timer("boadicea_deselect", 20);
  }

  trigger Ping_Boadicea( sound_finished () && !object_selected(HUMAN, Boadicea_id ) ) {
    ping_object (1, 1, 1, Boadicea_id);
    set_timer("progress_check",objective_delay);
  }

  trigger talk_about_actions( object_selected(HUMAN, Boadicea_id ) && sound_finished() ) {
    clear_all_arrows();
    options_display_right = get_button_right("left_window");
    options_display_top  = get_button_top("left_window");
    draw_screen_arrow_line( options_display_right + 100, options_display_top - 100, options_display_right, options_display_top, 0 );
    // WAV: The bottom left part of the screen shows the actions that the selected unit can perform.
    play_sound( "action_area.wav");
    enable_trigger( "interested_in_movement");
  }

  trigger interested_in_movement( object_selected(HUMAN, Boadicea_id ) && sound_finished() ) {
    clear_all_arrows();
    // highlight move button
    // WAV: We can use the move button to start Boadicea on her way back to her village.
    play_sound( "using_move_button.wav");
    enable_trigger( "click_on_move_button" );
    set_timer("progress_check",objective_delay);
  }

  trigger click_on_move_button( object_selected(HUMAN, Boadicea_id ) && sound_finished() ) {
    // WAV: Click on the move button in the bottom left.
    highlight_option("Move");
    add_objective( $S("Click on the move button."), "click_on_move", "click_on_move.wav");
    play_objective_sound("click_on_move");
    enable_trigger( "move_button_clicked" );
    set_timer("progress_check",objective_delay);
  }

  trigger move_button_clicked( sound_finished() && is_move_selected() ) {
    clear_highlight();
    objective_complete("click_on_move");
    // WAV: Now left click on the flag to have Boadicea move there.
    add_objective($S("Left click on the flag to have Boadicea move there."), "click_on_flag", "click_on_flag.wav");
    play_objective_sound("click_on_flag");
    enable_trigger( "moved_to_flag" );
    set_timer("progress_check",objective_delay);
  }

  trigger moved_to_flag( object_near( HUMAN, Boadicea_id, first_flag_x, first_flag_y, 2 ) ) {
    clear_all_arrows();
    disable_trigger("talk_about_actions");
    disable_trigger("move_button_clicked");
    disable_trigger("click_on_move_button");
    disable_trigger("interested_in_movement");
    clear_highlight();
    clear_flags();
    add_flag( second_flag_x, second_flag_y );
    objective_complete("click_on_move");
    objective_complete("click_on_flag");
    // WAV: Good job!
    play_sound("move_successful.wav");
    enable_trigger( "look_to_the_north" );
    enable_trigger("arrived_at_second_flag");
    enable_trigger("scrolled_up");
    set_timer("progress_check",objective_delay);
  }

  trigger look_to_the_north( sound_finished() ) {
    // WAV: We know our next destination is above us but out of our current view.  We want to move our view up a little 
  // to see that area of the map.
    play_sound("move_view_up.wav");
    enable_trigger("to_change_your_view");
    set_timer("progress_check",objective_delay);
  }
  
  trigger to_change_your_view( sound_finished() ) {
    // WAV: You can change your view on the map by moving your mouse to the edge of the screen.  This is called scrolling
    play_sound("intro_scrolling.wav");
    enable_trigger("move_scroll_up");
    set_timer("progress_check",objective_delay);
  }

  trigger move_scroll_up( sound_finished() ) {
    middle_of_screen = get_screen_width() / 2;
    draw_screen_arrow_line( middle_of_screen, 100, middle_of_screen, 0, 0 );

    add_objective($S("Move your mouse to the top edge of the screen to start scrolling up."), "scroll_up", "scroll_up.wav");
    play_objective_sound("scroll_up");
    // WAV:  Move your mouse to the very top edge of the screen and a little beyond to start scrolling up.

    enable_trigger("scrolled_up");    
    set_timer("progress_check",objective_delay);
  }

  trigger scrolled_up( is_on_screen( second_flag_x, second_flag_y ) ) {
    disable_trigger("to_change_your_view");
    disable_trigger("move_scroll_up");
    disable_trigger("look_to_the_north");
    clear_all_arrows();
    ping(1, 1, second_flag_x, second_flag_y );
    objective_complete("scroll_up");
    // WAV:  Good - now we can see our next destination on the other side of some unexplored territory.
    play_sound("scrolling_complete.wav");

    enable_trigger("the_undiscovered_country");
    set_timer("progress_check",objective_delay);
  }

  trigger the_undiscovered_country( sound_finished() ) {
    // WAV:  Unexplored territory is represented by dark areas on the map.  We can explore the map by having our
    //       units move through these dark areas.
    play_sound("unexplored_territory.wav");
    enable_trigger("select_move_again"); 
    set_timer("progress_check",objective_delay);
  }

  trigger select_move_again( sound_finished() ) {
    highlight_option("Move");
    add_objective($S("Select the move button"), "select_move_button", "select_move_again.wav");
    play_objective_sound( "select_move_button" );
    // WAV:  Select the move button again.
    enable_trigger( "left_click_on_flag" );
    set_timer("progress_check",objective_delay);
  }

  trigger left_click_on_flag( is_move_selected() && sound_finished() ) {
    clear_highlight();
    objective_complete("select_move_button");

    // WAV:  Now left click on the flag on the other side of the unexplored area.
    add_objective($S("Left click on the flag on the other side of the unexplored area."), "left_click_on_flag", "left_click_on_unexplored_flag.wav");
    play_objective_sound("left_click_on_flag");

    enable_trigger("arrived_at_second_flag");
    set_timer("progress_check",objective_delay);
  }

  trigger arrived_at_second_flag( object_near( HUMAN, Boadicea_id, second_flag_x, second_flag_y, 2 ) ) {
    clear_flags();
    clear_highlight();
    disable_trigger("left_click_on_flag");
    disable_trigger("select_move_again");
    disable_trigger("the_undiscovered_country");
    disable_trigger("move_scroll_up");
    disable_trigger("scrolled_up");
    disable_trigger("to_change_your_view");
    disable_trigger("look_to_the_north");
    add_flag( city_flag_x, city_flag_y );
    objective_complete("left_click_on_flag");
    objective_complete("select_move_button");
    objective_complete("scroll_up");
    // WAV:  Good, we are getting closer to home.
    play_sound("closer_to_home.wav");
    enable_trigger("now_look_for_briton");
    set_timer("progress_check",objective_delay);
  }

  trigger now_look_for_briton( sound_finished() ) {
    // WAV:  We need to find Briton on the map.
    play_sound("move_view_to_briton.wav");
    enable_trigger("help_in_search");
    set_timer("progress_check",objective_delay);
  }

  trigger help_in_search( sound_finished() ) {
    // WAV:  We can make our search easier by zooming out our view.
    play_sound("mention_zoom.wav");
    enable_trigger("Learn_zoom");
    set_timer("progress_check",objective_delay);
  }

  trigger Learn_zoom (sound_finished ()) {
    scrolling_lock ();
    // We can zoom out our view by rolling the mousewheel backward or pressing the page up key twice.
    add_objective($S("Roll the mousewheel back or press PageUp twice to zoom out."), "zoom_out", "learnzoom.wav");
    play_objective_sound("zoom_out");
    enable_trigger ("Zoom_out");
    enable_trigger ("zoom_middle");
    set_timer("zoom_out_timer", 10);
    set_timer("progress_check",objective_delay);
  }

  trigger zoom_middle ( (get_current_zoom() == 5) && timer_expired("zoom_out_timer") ) {
    // WAV: You have zoomed out a little, but we need to zoom all the way out to get the best view.
    //      Roll the mousewheel backward again or press the page up key again.
    play_sound("zoom_out_again.wav");
  }

  trigger Zoom_out( is_zoomed_out() ) {
    disable_trigger("zoom_middle");
    scrolling_unlock();
    objective_complete("zoom_out");

    // WAV:  Now we have a better view to search for Briton.
    play_sound("better_view.wav");
    enable_trigger("what_we_know");
  }

  trigger what_we_know( sound_finished() ) {
    // WAV:  Briton is above us and a little to the right.
    play_sound("general_briton_directions.wav");
    enable_trigger("scroll_to_briton");
  }

  trigger scroll_to_briton( sound_finished() ) {
    draw_screen_arrow_line (get_screen_width() - 100, 100, get_screen_width(), 0, 10 );
    add_objective($S("Scroll to the top right of the screen to find Briton."), "find_briton", "scroll_to_find_briton.wav");
    play_objective_sound("find_briton");
    // WAV:  Scroll up and a little to the right to find Briton.
    enable_trigger("found_home");
    enable_trigger("found_camp");
  }

  trigger found_home( is_on_screen (object_position_x(HUMAN, BRITON_ID)-1, object_position_y(HUMAN, BRITON_ID)-1)) {
    disable_trigger("found_camp");
    clear_all_arrows();
    objective_complete("find_briton");
    // WAV:  We located Briton!  Our army is waiting to be led into battle!
    play_sound("located_briton.wav");
    enable_trigger("you_can_now_go_home");
  }

  trigger found_camp( is_on_screen (R_Barracks_X+10, R_Barracks_Y+10) && is_on_screen( R_Barracks_X, R_Barracks_Y) ) {
    clear_all_arrows();
    // WAV: This is the Roman camp.  We are looking for the city of Briton that is up and to the left of this position.
    play_sound("found_roman_camp.wav");
    draw_screen_arrow_line( 100, 100, 0, 0, 0 );
  }

  trigger you_can_now_go_home( sound_finished() ) {
    // WAV:  Move Boadicea to the flag right outside the village.  You can use the 
    //       move button to direct her again, or you can right click on the flag as 
    //       a shortcut.
    add_objective($S("Move Boadicea to the flag outside her village."), "move_Boadicea_home", "move_Boadicea_to_briton.wav");
    play_objective_sound("move_Boadicea_home");
    enable_trigger("Knows_moving");
  }

  /*
  trigger Move_a_unit( sound_finished () &&  object_selected(HUMAN, Boadicea_id ) ) {
    clear_all_arrows();
    add_objective($S("Select Boadicea and right click on the flag."), "Move_Boadicea", "move_Boadicea.wav");
    play_objective_sound("Move_Boadicea");
    //WAV: "To move a unit, select it and then right-click on the map where you want it to go. 
    //      With Boadicea selected, move her toward the flag in the upper right.
    add_flag (FLAG_X, FLAG_Y);
    enable_trigger("Flag");
    enable_trigger("reveal_flag");
    set_timer("progress_check",objective_delay);
  }

  trigger reveal_flag(sound_finished() ) {
    set_explored( HUMAN, FLAG_X, FLAG_Y, 5 );   
  }

  trigger Flag(object_near (HUMAN, Boadicea_id, FLAG_X, FLAG_Y, 2)) {
    disable_trigger("reveal_flag");
    objective_complete("Move_Boadicea");
    clear_flags ();
    play_sound ("at_flag.wav");
    // WAV: "Very good. Now we'll guide your Boadicea back to the village and rally the troops."
    disable_trigger("Deselect_Boadicea");
    enable_trigger ("Learn_scroll");
    set_timer("progress_check",objective_delay);
  }

  trigger Learn_scroll (sound_finished ()) {
    // WAV: Briton is off the screen to the upper right.  To see off the screen, move the mouse cursor to the edge of the screen and a little beyond.
    //      This is called scrolling.  Scroll to find Briton in the upper right now.
    add_objective($S("Locate Briton by scrolling to the upper right."), "Find_briton", "learnscroll.wav");
    draw_screen_arrow_line (get_screen_width() - 100, 100, get_screen_width(), 0, 10 );
    play_objective_sound("Find_briton");  
    set_timer("progress_check",objective_delay);
    enable_trigger("camera_over_village");
  }

  trigger camera_over_village( is_on_screen (object_position_x(HUMAN, BRITON_ID)-1, object_position_y(HUMAN, BRITON_ID)-1)){
    clear_all_arrows();
    objective_complete("Find_briton");
    // wav:  Very good.  You found the city of Briton.  Now, right click to move Boadicea to the flag outside the village.
    add_objective($S("Right click to move your unit to the flag."), "Move_to_briton", "move_b_village.wav");
    add_flag (FLAG2_X, FLAG2_Y);
    enable_trigger("Deselect_Boadicea");
    play_objective_sound("Move_to_briton");
    enable_trigger("Knows_moving");
  }
  */

  trigger Knows_moving (object_near_build (HUMAN, Boadicea_id, HUMAN, BRITON_ID, 8)) {
    objective_complete("move_Boadicea_home");
    clear_flags();
    play_sound ("Boadicearejoins.wav");
    //WAV: "Good work! You've successfully guided Boudica back to Briton so we can start rallying our troops. But before we attack, we must first we must find our hated foe."
    disable_all_triggers ();
    disable_trigger("Deselect_Boadicea");
    disable_trigger("boadicea_lost");
    enable_trigger ("Global");
    enable_trigger ("player_lost");
    enable_trigger ("look_for_camp");
    enable_trigger ("Objective_Reminder");
    set_timer("progress_check",objective_delay);
  }

  trigger look_for_camp( sound_finished() ) {
    add_objective($S("Scroll to the bottom right to find the Roman camp."), "find_camp", "locate_camp.wav");
    play_objective_sound("find_camp");
    // WAV:  Scroll to the bottom right to find the Roman camp.
    draw_screen_arrow_line (get_screen_width() - 100, get_screen_height() - 100, get_screen_width(), get_screen_height(), 10 );
    enable_trigger ("Find_camp");    
  }

  trigger Find_camp ( is_on_screen (R_Barracks_X+10, R_Barracks_Y+10) && is_on_screen( R_Barracks_X, R_Barracks_Y) ) {
    clear_all_arrows();
    //ping_object (1, 1, 2, barracks_id);
  
    mid_x = ( R_Barracks_X + lookout_x ) / 2;
    mid_y = ( R_Barracks_Y + lookout_y ) / 2;

    ping_constant(1, mid_x, mid_y );
    add_flag( mid_x, mid_y );
    objective_complete("find_camp");
    play_sound ("foundcamp.wav");
    //WAV: "Excellent, you've found the Roman camp. Now let's take a"
    //     "closer look at their defenses."
    enable_trigger ("Mouseover");
    set_timer("progress_check",objective_delay);
  }

  trigger Mouseover (sound_finished ()) {
    clear_flags();
    clear_pings(HUMAN);
    ping_object (1, 1, 2, barracks_id);
    //WAV: Moving the mouse over anything in the game will show you the help text in the lower
    //     left corner.  Move your mouse over the Roman Barracks.
    add_objective($S("Move your mouse over the Roman barracks"), "over_barracks", "mouseover.wav" );
    play_objective_sound("over_barracks");
    //enable_trigger("Over_tower");
    enable_trigger("check_over_barracks");
    set_timer("progress_check",objective_delay);
  }
  
  trigger check_over_barracks( get_object_type_at_mouse( COMPUTER, "Barracks" ) ) {
    set_timer( "barracks_check", 1 );
    enable_trigger("Over_barracks");
  }

  trigger Over_barracks( sound_finished() && get_object_type_at_mouse (COMPUTER, "Barracks") && timer_expired("barracks_check") ) {
    objective_complete("over_barracks");
    //disable_trigger("Over_tower");
    play_sound ("overbarracks.wav");
  /***WAV: "Notice the help text in the lower left--it says this building is used to produce troops.
  We'd better destroy it quickly! Let's get our units organized and attack!"***/
    enable_trigger("scouting_complete");
  }

  trigger scouting_complete( sound_finished() ) {
    play_sound("knowrposition.wav");
          //WAV: Excellent! Now you know a lot about the Roman position. Let's get our units organized, and attack.

    enable_trigger("Attack_the_Romans");
    set_timer("progress_check", objective_delay);
  }

  trigger Attack_the_Romans (sound_finished ()) {
    center_camera_on (HUMAN, find_military (HUMAN));
    add_objective($S("Select all troops with a drag box."), "all_troops", "multiselect2.wav");
    play_objective_sound("all_troops");
  /***WAV: "You can select your whole army at once with a drag box."
      "Hold the mouse near your units, left-click, then drag the mouse
      "to create a box around all your troops."***/
    enable_trigger ("All_troops");
    enable_trigger ("Not_all_troops");
    set_timer("progress_check",objective_delay);
  }

  trigger Not_all_troops ( (num_objects_selected (HUMAN) > 1) && (num_objects_selected(HUMAN) < num_units (HUMAN) )) {
    play_sound ("notall.wav");
  /***WAV: You selected some, but not all, of your troops. Try again, and make a bigger box.***/
    set_timer("progress_check",objective_delay);
  }

  trigger All_troops (all_military_selected (HUMAN) && object_selected( HUMAN, Boadicea_id ) ) {
    disable_trigger ("Not_all_troops");
    objective_complete("all_troops");
    add_objective($S("Right-click near the Roman camp to move your troops into the camp."),"destroy_camp", "alltroops.wav");
    play_objective_sound("destroy_camp");
    
    mid_x = ( R_Barracks_X + lookout_x ) / 2;
    mid_y = ( R_Barracks_Y + lookout_y ) / 2;

    ping_constant(1, mid_x, mid_y );
    add_flag( mid_x, mid_y );
  /***WAV: "Good work. You successfully selected all of your troops.  Now send"
      "your troops toward the Roman position. If they stop close to an enemy building"
      "or unit, they'll attack automatically. Destroy the Roman camp.  We'll teach them"
      "to march into our town!"***/
    enable_trigger ("Deselect_army");
    set_timer("deselect_timer", 1 );
    enable_trigger("Destroy_camp");
    set_timer("progress_check",objective_delay);
  }

  trigger Global (have_fought (HUMAN, COMPUTER)) {
    
    clear_flags ();
    clear_pings(HUMAN);
    clear_all_arrows ();
    scrolling_unlock ();
    create_unit(COMPUTER, R_Barracks_X - 4, R_Barracks_Y, "Legions", 2);

    if (is_trigger_enabled ("Destroy_camp")) {
      play_sound ("attackbegun.wav");
      //WAV: "The attack is begun! To attack specific targets, select the attacking
      //unit and right click on an enemy.  If you want a better view of the action, 
      // you can zoom in by rolling the mouse wheel forward or pressing the page down key multiple times.
      enable_trigger ("See_health");
      enable_trigger ("Deselect_army");
    } else {
      play_sound ("earlyattack.wav");
  /***WAV: Good work General. Apparently you don't need any help in order to conquer the Romans. Good luck!***/
      clear_objectives();
      disable_all_triggers ();
      enable_trigger ("Destroy_camp");
    }

    objective_complete("destroy_camp");
    add_objective($S("Destroy Roman army"),     "destroy_army",     "alltroops.wav" );
    add_objective($S("Destroy Roman barracks"), "destroy_barracks", "alltroops.wav");
    add_objective($S("Destroy Roman lookout"),  "destroy_lookout",  "alltroops.wav");
    enable_trigger ("Destroy_tower");
    enable_trigger ("Destroy_barracks");
    enable_trigger ("Destroy_army");
    set_timer("progress_check",objective_delay);
  }

  trigger Deselect_army (sound_finished () && (num_objects_selected (HUMAN) == 0 ) && timer_expired("deselect_timer") ) {
    play_sound ("lostarmy.wav");
  /***WAV: You have troops that are not selected who are not attacking. Select them with a left click or
      drag box and get them in the fight!***/
    enable_trigger("Deselect_army");
    set_timer("deselect_timer", 20);
  }

  trigger See_health (sound_finished ()) {
    play_sound ("damage.wav");
  /***WAV: As the fight progresses, you can see units and buildings taking damage. Each selected unit and building has a green
      bar above it when it's at full strength. As they are hit by the enemy, this bar shrinks and becomes yellow and then
      red. When the health bar is all gone, the unit or building is destroyed.***/
    set_timer("progress_check",objective_delay);
  }

  trigger Destroy_tower (building_type_destroyed (COMPUTER, "Lookout")) {
    tower_destroyed = true;
    objective_complete("destroy_lookout");
  }

  trigger Destroy_barracks (building_type_destroyed (COMPUTER, "Barracks")) {
    barracks_destroyed = true;
    objective_complete("destroy_barracks");
  }

  trigger Destroy_army( num_military_units(COMPUTER) < 1 ) {
    army_destroyed = true;
    objective_complete("destroy_army");
  }

  trigger Destroy_camp (tower_destroyed && barracks_destroyed && army_destroyed) {
    disable_all_triggers ();
    play_sound ("congrats.wav");
  /***WAV: Congratulations!  You've leveled the Roman camp.  But it won't be long before"
      "they'll be back in strength.  We'd best be ready.***/
    enable_trigger ("Bonus");
    disable_trigger("Objective_Reminder");
  }
  //==========================================================


  //Putting it all together===================================
  trigger Bonus (sound_finished ()) {
    set_timer ("Help", 4);
    Legions = create_unit (COMPUTER, BONUS_LEGIONS_X, BONUS_LEGIONS_Y, "Legions", 1);
    unit_attack_order(COMPUTER, Legions, HUMAN, BRITON_ID);
    zoom_in_on(HUMAN, BRITON_ID);
    enable_trigger ("Help_out");
    enable_trigger ("The_end");
  }

  trigger Help_out (timer_expired ("Help")) {
  /***WAV: The vile Romans have launched a sneak attack against Briton! We must come to her aid.***/
    center_camera_on(HUMAN, find_military(HUMAN) );
    add_objective($S("Briton is under attack! Right-click to move your troops to defend the city!"), "sneak_attack", "sneakattack.wav");
    play_objective_sound("sneak_attack");
    enable_trigger ("Reinforce");
    enable_trigger("Deselect_army");
  }

  trigger Reinforce (num_military_units (HUMAN) < 1) {
    create_unit (HUMAN, ARMY_CREATE_X, ARMY_CREATE_Y, "Hoplites", 7);
    for (i = 0; i < num_military_units (HUMAN); i++) {
      ID = find_military (HUMAN);
      unit_attack_to_order (HUMAN, ID, ATTACK_TO_X, ATTACK_TO_Y);
    }
  }

  trigger The_end (num_military_units (COMPUTER) < 1) {
    objective_complete("sneak_attack");
    play_sound ("victory.wav");
    /***WAV: Congratulations! You have fended off the evil Romans***/
    enable_trigger("The_real_end");
  }

  trigger The_real_end( sound_finished() ) {
    victory( HUMAN );
  }
  //==========================================================

  trigger player_lost( num_military_units( HUMAN ) <= 0 ) {
    VICTORY( COMPUTER );
  }

  trigger boadicea_lost( unit_killed( HUMAN, Boadicea_id ) ) {
    VICTORY( COMPUTER );
  }
}
