scenario
{

labels {
}

static diff = get_difficulty();
static rate_guy = 0;
static int samnium_splayed = 0;
static int picenum_splayed = 0;
static int umbrians_splayed = 0;
static int etruscans_splayed = 0;

run_once {
  disable_all_triggers();
  //disable_city_ai(2);
  disable_city_ai(3);
  disable_city_ai(4);
  disable_city_ai(5);
  disable_city_ai(6);
  disable_city_ai(7);
  
  enable_trigger("Rome_Init");
  enable_trigger("Etruscans_Init");
  enable_trigger("Samnites_Init");
  enable_trigger("Umbrians_Init");

  enable_trigger("Samnites_Defeated");
  enable_trigger("Umbrians_Defeated");
  enable_trigger("Picenum_Defeated");
  enable_trigger("Etruscans_Defeated");

  enable_trigger("Guidance_Samnium");
  enable_trigger("WreckEtruscans");

  add_visibility(1, 4);
  enable_trigger("take_library1");
  enable_trigger("take_library2");
}

trigger take_library1(have_tech(5, "Standing Army")) {
  disable_type_by_tribe("Library", "Spanish");
  destroy_building_type(2, "Library");
}
trigger take_library2(have_tech(3, "Mercenaries")) {
  disable_type_by_tribe("Library", "Germans");
  destroy_building_type(3, "Library");
}

trigger Rome_Init()
{
//182, 140 -> 229, 176
  clear_group(2);
  create_unit(2, 182, 140, "Javelineers", 6);
  create_unit_in_group(2, 182, 140, "Phalanx", 10);
  create_unit_in_group(2, 182, 140, "Archers", 6);
  create_unit_in_group(2, 182, 140, "Light Horse", 4);
  create_unit_in_group(2, 182, 140, "Cataphract", 4);
  create_unit_in_group(2, 182, 140, "Catapult", 4);
  create_unit_in_group(2, 182, 140, "Supply Wagon", 1);
  group_attack_to_order(2, 229, 176);
//116, 90 -> 81, 64
  clear_group(2);
  create_unit(2, 116, 90, "Javelineers", 2);
  create_unit_in_group(2, 116, 90, "Phalanx", 2);
  create_unit_in_group(2, 116, 90, "Archers", 2);
  create_unit_in_group(2, 116, 90, "Light Horse", 2);
  create_unit_in_group(2, 116, 90, "Cataphract", 2);
  group_attack_to_order(2, 81, 64);
}

trigger Samnites_Init()
{
//279, 134 -> 251, 152
  create_unit(3, 279, 134, "Javelineers", 2);
  create_unit_in_group(3, 279, 134, "Phalanx", 4);
  create_unit_in_group(3, 279, 134, "Archers", 6);
  create_unit_in_group(3, 279, 134, "Light Horse", 3);
  create_unit_in_group(3, 279, 134, "Cataphract", 1);
  create_unit_in_group(3, 279, 134, "Catapult", 2);
  group_attack_to_order(3, 251, 152);
}

trigger Etruscans_Init()
{
//81, 64 -> 116, 90
  create_unit(5, 81, 64, "Javelineers", 2);
  create_unit_in_group(5, 81, 64, "Phalanx", 2);
  create_unit_in_group(5, 81, 64, "Archers", 2);
  create_unit_in_group(5, 81, 64, "Light Horse", 2);
  create_unit_in_group(5, 81, 64, "Cataphract", 2);
  group_attack_to_order(5, 116, 90);
}

trigger Umbrians_Init()
{
//215, 47 -> 251, 152
  create_unit(6, 215, 47, "Javelineers", 2);
  create_unit_in_group(6, 215, 47, "Phalanx", 1);
  create_unit_in_group(6, 215, 47, "Archers", 1);
  create_unit_in_group(6, 215, 47, "Light Horse", 2);
  create_unit_in_group(6, 215, 47, "Cataphract", 1);
  group_attack_to_order(6, 251, 152);
}

trigger Samnites_Defeated(is_defeated(3))
{
  enable_trigger("Samnite_Power_Give");
}

trigger Samnite_Power_Give(find_city_owner("Saepinum") == 1)
{
  //tribute of 100 of each resource every five mins
  enable_trigger("Resource_Give");
  set_timer("RG", 10);
  enable_trigger("Samnite_Power_Take");
}

trigger Resource_Give(timer_expired("RG"))
{
  if (samnium_splayed < 1) {
    popup_dialog($S("Since you have taken all of their cities, the Samnites acknowledge your rule. The remaining citizens agree to give you a portion of their resources. All of your resource gather rates are increased by 100."));
    samnium_splayed = 1;
  }
  yo = get_base_rate(1, "Food");
  set_base_rate(1, "Food", yo+100);

  yo = get_base_rate(1, "Timber");
  set_base_rate(1, "Timber", yo+100);

  yo = get_base_rate(1, "Metal");
  set_base_rate(1, "Metal", yo+100);

  yo = get_base_rate(1, "Wealth");
  set_base_rate(1, "Wealth", yo+100);
  enable_trigger("Resource_Give");
}

trigger Samnite_Power_Take(find_city_owner("Saepinum") != 1)
{
  yo = get_base_rate(1, "Food");
  set_base_rate(1, "Food", yo-100);

  yo = get_base_rate(1, "Timber");
  set_base_rate(1, "Timber", yo-100);

  yo = get_base_rate(1, "Metal");
  set_base_rate(1, "Metal", yo-100);

  yo = get_base_rate(1, "Wealth");
  set_base_rate(1, "Wealth", yo-100);
  enable_trigger("Samnite_Power_Give");
}

trigger Picenum_Defeated(is_defeated(7))
{
  enable_trigger("Picenum_Power_Give");
}

trigger Picenum_Power_Give(find_city_owner("Asculum") == 1)
{
  //entire map is revealed
  if (picenum_splayed < 1) {
    popup_dialog($S("While in Asculum you take control of a Picenum spy ring which reveals the whole map!"));
    picenum_splayed = 1;
  }
  show_all_map_enable(1);
  enable_trigger("Picenum_Power_Take");
}

trigger Picenum_Power_Take(find_city_owner("Asculum") != 1)
{
  show_all_map_disable(1);
  enable_trigger("Picenum_Power_Give");
}

trigger Umbrians_Defeated(is_defeated(6))
{
  enable_trigger("Umbrians_Power_Give");
}

trigger Umbrians_Power_Give(find_city_owner("Terni") == 1)
{
  //metal bonus? raise commerce cap to 999 and double gather rate
  if (umbrians_splayed < 1) {
    popup_dialog($S("Special mining tools have been found in Terni. Your Metal#ICON5 production has increased."));
    umbrians_splayed = 1;
  }
  if (!resource_cap_warning(1, "Metal")) {
    if (rate_guy != (gather_rate(1, "Metal") - get_base_rate(1, "Metal"))) {
      rate_guy = gather_rate(1, "Metal") - get_base_rate(1, "Metal");
      set_base_rate(1, "Metal", rate_guy);
    }
  }
  set_bonus_cap(1, "Metal", 999);
  enable_trigger("Umbrians_Power_Take");
}

trigger Umbrians_Power_Take(find_city_owner("Terni") != 1)
{
  set_base_rate(1, "Metal", 0);
  set_bonus_cap(1, "Metal", 0);
  enable_trigger("Umbrians_Power_Give");
}

trigger Etruscans_Defeated(is_defeated(5))
{
  enable_trigger("Etruscans_Power_Give");
}

trigger Etruscans_Power_Give(find_city_owner("Tarquinii") == 1)
{
  //cause desertions - lower pop cap for other nations - 70 pop cap
  if (etruscans_splayed < 1) {
    popup_dialog($S("Your control over the capital of the powerful Etruscans has demoralized the enemy's troops. Their armies can no longer regain their former strength."));
    etruscans_splayed = 1;
  }
  if (population_cap(2) > 70) {
    set_population_cap(2, 70);
  }
  if (population_cap(3) > 70) {
    set_population_cap(3, 70);
  }
  if (population_cap(4) > 70) {
    set_population_cap(4, 70);
  }
  if (population_cap(5) > 70) {
    set_population_cap(5, 70);
  }
  if (population_cap(6) > 70) {
    set_population_cap(6, 70);
  }
  if (population_cap(7) > 70) {
    set_population_cap(7, 70);
  }

  enable_trigger("Etruscans_Power_Give");
  enable_trigger("Etruscans_Power_Take");
}

trigger Etruscans_Power_Take(find_city_owner("Tarquinii") != 1)
{
  enable_trigger("Etruscans_Power_Give");
}

trigger Guidance_Samnium()
{
  if (!is_defeated(3)) {
    ctw_add_objective($S("Hint: Conquer the Samnites (Purple)."), "SamniumGoal", "");
    enable_trigger("GS_Complete");
  }
  else {
    enable_trigger("Guidance_Picenum");
  }
}

trigger GS_Complete(is_defeated(3))
{
  ctw_objective_complete("SamniumGoal");
  enable_trigger("Guidance_Picenum");
}

trigger Guidance_Picenum()
{
  if (!is_defeated(7) && !have_alliance(1, 7)) {
    ctw_add_objective($S("Hint: Conquer the Picenum (White)."), "PicenumGoal", "");
    city_id = find_city_id("Asculum");
    set_seen(1, 7, city_id);
    enable_trigger("PS_Complete");
  }
  else {
    enable_trigger("Guidance_Umbrians");
  }
}

trigger PS_Complete(is_defeated(7))
{
  ctw_objective_complete("PicenumGoal");
  enable_trigger("Guidance_Umbrians");
}

trigger Guidance_Umbrians()
{
  if (!is_defeated(6)) {
    ctw_add_objective($S("Hint: Conquer the Umbrians (Light Blue)."), "UmbriaGoal", "");
    enable_trigger("US_Complete");
  }
  else {
    enable_trigger("Guidance_Etruscans");
  }
}

trigger US_Complete(is_defeated(6))
{
  ctw_objective_complete("UmbriaGoal");
  enable_trigger("Guidance_Etruscans");
}

trigger Guidance_Etruscans()
{
  ctw_add_objective_text($S("Capture the remaining enemy capital(s)."), "Hint_id", "");
}

trigger WreckEtruscans()
{
  if (population_cap(5) > 85) {
    set_population_cap(5, 85);
  }
  enable_trigger("WreckEtruscans");
}

if(time() >= get_time_limit()) {
  defeat(1);
}

}