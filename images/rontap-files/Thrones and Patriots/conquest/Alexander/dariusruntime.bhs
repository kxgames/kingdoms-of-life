 scenario
{

labels {
  //Add any constant labels here in a comma separated list.
}

static alexander_id = 0;
static darius_id = 0;
static x = 0;
static y = 0;
static fx = 0;
static fy = 0;

run_once {
  disable_all_triggers();
  
  alexander_id = find_unit(1, "Alexander");
  darius_id = find_unit(2, "Darius");
  
  force_transport_ability(2);

  enable_trigger("Darius_Init");
  enable_trigger("Darius_Flees");
  if(get_country_owner("Mesopotamia") == "greeks") {
    ctw_add_objective_text($S("Darius III has not learned his lesson! Another charge by Alexander should get the message across."), "First_Obj", "");
  }
  else {
    ctw_add_objective_text($S("Darius III is a cautious commander, so a decisive charge by Alexander could break his resolve."), "First_Obj", "");
  }
}

trigger Darius_Init()
{
  clear_group(2);
  add_to_group(2, darius_id);
  unit_id = find_unit(2, "Supply Wagon");
  group_guard_order(2, 2, unit_id);
  clear_group(2);
  add_to_group(2, unit_id);
  for (i = 0; i < num_military_units(2); i++) {
    unit_id = find_military(2);
    add_to_group(2, unit_id);
  }
  for (i = 0; i < num_type(2, "Transport"); i++) {
    unit_id = find_unit(2, "Transport");
    add_to_group(2, unit_id);
  }
  x = get_starting_loc_x(1);
  y = get_starting_loc_y(1);
  group_attack_to_order(2, x, y);
}

trigger Darius_Flees(object_near(1, alexander_id, object_position_x(2, darius_id), object_position_y(2, darius_id), 4) || num_military_units(2) < 5 || object_health(2, darius_id) < 80)
{
  if(get_country_owner("Mesopotamia") == "greeks") {
    popup_dialog($S("Darius III's Royal Guard has failed him again! He is fleeing the battlefield yet again!"));
  }
  else {
    popup_dialog($S("Darius III's Royal Guard - the so-called Immortals - have failed him! He is fleeing from Alexander's charge and his army is fleeing with him!"));
  }
  ctw_remove_objective("First_Obj");
  ctw_add_objective_text($S("Capture the enemy's capital(s)."), "Second_Obj", "");

  city_name = find_capital(2);
  city_id = find_city_id(city_name);
  set_seen(1, 2, city_id);

  dx = get_starting_loc_x(2);
  dy = get_starting_loc_y(2);
  if (dx < world_x_size()/2) {
    if (dy < world_y_size()/2) {
      //flee to 0, 0
      fx = 0;
      fy = 0;
    }
    else {
      //flee to 0, worldysize
      fx = 0;
      fy = world_y_size()-3;
    }
  }
  else {
    if (dy < world_y_size()/2) {
      //flee to worldxsize, 0
      fx = world_x_size()-3;
      fy = 0;
    }
    else {
      //flee to worldxsize, worldysize
      fx = world_x_size()-3;
      fy = world_y_size()-3;
    }
  }
  add_to_group(2, darius_id);
  group_move_order(2, fx, fy);
  enable_trigger("Units_Leave");
}

trigger Units_Leave(any_object_near(2, 1, fx, fy, 15))
{
  for (i = 0; i < num_military_units(2); i++) {
    unit_id = find_military(2);
    if (object_near(2, unit_id, fx, fy, 15)) {
      kill_unit_anim(2, unit_id, 1);
    }
  }
  
  if (object_near(2, darius_id, fx, fy, 15)) {
    kill_unit_anim(2, darius_id, 1);
  }
  
  unit_id = find_unit(2, "Supply Wagon");
  if (object_near(2, unit_id, fx, fy, 15)) {
    kill_unit_anim(2, unit_id, 1);
  }

  for (i = 0; i < 10; i++) {
    unit_id = find_unit(2, "Transport Barge");
    if (object_near(2, unit_id, fx, fy, 15)) {
      kill_unit_anim(2, unit_id, 1);
    }
  }
  
  if (num_units(2) > 10) {
    enable_trigger("Units_Leave");
  }
}

if(time() >= get_time_limit()) {
  defeat(1);
}

}