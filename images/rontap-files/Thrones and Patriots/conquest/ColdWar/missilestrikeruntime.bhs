 conquest
{

static int diff = get_difficulty();

static int refinery = 0;
static int ow_1 = 0;
static int ow_2 = 0;
static int ow_3 = 0;

static int silo_1 = 0;
static int silo_2 = 0;
static int silo_3 = 0;
static int silo_4 = 0;

static int ref_obj = 0;
static int ow_obj = 0;
static int silo_obj = 0;

static int commando_1 = 0;
static int commando_2 = 0;

run_once {
  ctw_add_objective($S("Destroy the Refinery in Sevastopol."), "Sev_id", 0);
  ctw_add_objective($S("Destroy the Oil Wells in the central region of the peninsula."), "OW_id", 0);
  ctw_add_objective($S("Destroy the Missile Silos located somewhere northeast of Simferopol."), "Silo_id", 0);
  ctw_add_objective_text($S("Use your Spies to call in Missile Strikes."), "Hint2_id", 0);
  set_timer("STRIKE", 5);
  if (diff > 2) {
    set_timer("PATROL", 5);
  }

  refinery = find_build(2, "Refinery");

  ow_1 = find_build(2, "Oil Well");
  ow_2 = find_build(2, "Oil Well");
  ow_3 = find_build(2, "Oil Well");

  silo_1 = find_build(2, "Missile Silo");
  silo_2 = find_build(2, "Missile Silo");
  silo_3 = find_build(2, "Missile Silo");
  silo_4 = find_build(2, "Missile Silo");

  rename_type("Informer", $S("Missile Strike"));

  x = 118;
  y = 162;
  commando_1 = create_unit_upgrade(2, x, y, "Special Forces", 1);
  unit_patrol_order(2, commando_1, 70, 98);
  unit_patrol_order_queue(2, commando_1, 98, 78);

  x = 98;
  y = 78;
  commando_2 = create_unit_upgrade(2, x, y, "Special Forces", 1);
  unit_patrol_order(2, commando_2, 118, 162);
  unit_patrol_order_queue(2, commando_2, 70, 98);
}

trigger NoMoreSpies(num_units(1) < 1) {
  defeat(1);
}

if (timer_expired("STRIKE") != 0) {
  for (i = 0; i < num_buildings(2); i++) {
    build_id = find_build(2, "");
    if (is_infiltrated(2, build_id)) {
      x = 11;
      y = 199;
      create_missile_and_target_build(1, x, y, "Cruise Missile", 2, build_id);
      remove_infiltrated(2, build_id);
      show_notice($S("Cruise Missile launched!"), "launch_notice", 1);
      //silo_obj, ow_obj, ref_obj
      if (silo_obj == 1 && ow_obj == 1) {
        if (build_id == refinery) {
          disable_trigger("NoMoreSpies");
        }
      }
      else if (ow_obj == 1 && ref_obj == 1) {
        if (num_type(2, "Missile Silo") == 1) {
          if (build_id == silo_1 || build_id == silo_2 || build_id == silo_3 || build_id == silo_4) {
            disable_trigger("NoMoreSpies");
          }
        }
      }
      else if (silo_obj == 1 && ref_obj == 1) {
        if (num_type(2, "Oil Well") == 1) {
          if (build_id == ow_1 || build_id == ow_2 || build_id == ow_3) {
            disable_trigger("NoMoreSpies");
          }
        }
      }
    }
  }
  set_timer("STRIKE", 5);
}

if (timer_expired("PATROL")) {
  x = 0;
  y = 0;
  //rally points
  //118, 162 - oil refinery in sevastopol
  if (find_build(2, "Refinery") > -1) {
    x = 118;
    y = 162;
  }
  //70, 98 - oil wells in middle of island
  else if (find_build(2, "Oil Well") > -1) {
    x = 70;
    y = 98;
  }
  //98, 78 - missile silos in the north
  else {
    x = 98;
    y = 78;
  }
  build_id = find_build(2, "Auto Plant");
  set_rally(2, build_id, x, y);
  give_good(2, "Food", 200);
  give_good(2, "Oil", 200);
  train_unit_at(2, 1, "Armored Scout Car", build_id);
  set_timer("PATROL", 60);
}

trigger (building_destroyed(2, refinery)) {
  ctw_objective_complete("Sev_id");
  ref_obj = 1;
}

trigger (building_destroyed(2, ow_1) && building_destroyed(2, ow_2) && building_destroyed(2, ow_3)) {
  ctw_objective_complete("OW_id");
  ow_obj = 1;
}

trigger (building_destroyed(2, silo_1) && building_destroyed(2, silo_2) && building_destroyed(2, silo_3) && building_destroyed(2, silo_4)) {
  ctw_objective_complete("Silo_id");
  silo_obj = 1;
}

if (num_type_upgrade(2, "Oil Well") < 1 && num_type_upgrade(2, "Missile Silo") < 1 && num_type_upgrade(2, "Refinery") < 1) {
  if (get_total_nukes("Russians") > 0) {
    if (ctw_remove_nuke("Ukraine") > -1) {
      popup_dialog($S("As a result of our successful mission, a Nuclear Missile in Eastern Europe has been destroyed!"));
    }
    else if (ctw_remove_nuke("Karelia") > -1) {
      popup_dialog($S("As a result of our successful mission, a Nuclear Missile in Karelia has been destroyed!"));
    }
    else if (ctw_remove_nuke("Kazakhstan") > -1) {
      popup_dialog($S("As a result of our successful mission, a Nuclear Missile in Kazakhstan has been destroyed!"));
    }
    else if (ctw_remove_nuke("Russia") > -1) {
      popup_dialog($S("As a result of our successful mission, a Nuclear Missile in Western Russia has been destroyed!"));
    }
    else if (ctw_remove_nuke("Western Siberia") > -1) {
      popup_dialog($S("As a result of our successful mission, a Nuclear Missile in Central Russia has been destroyed!"));
    }
    else if (ctw_remove_nuke("Mongolia") > -1) {
      popup_dialog($S("As a result of our successful mission, a Nuclear Missile in Mongolia has been destroyed!"));
    }
    else if (ctw_remove_nuke("Eastern Siberia") > -1) {
      popup_dialog($S("As a result of our successful mission, a Nuclear Missile in Eastern Russia has been destroyed!"));
    }
  }
  else {
    popup_dialog($S("As a result of our successful mission, we have gained two Sabotage Bonus Cards."));
    give_player_bonus_card("Sabotage");
    give_player_bonus_card("Sabotage");
  }

  victory(1);
}

if (time() >= get_time_limit()) {
  defeat(1);
}

}
