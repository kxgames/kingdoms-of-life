       conquest
{
int x = 0;
int y = 0;
int general_id = 0;
static int diff = get_difficulty();
static num_bases_left = 0;

run_once {
  set_timer("GA", 2);
  set_timer("PG", 120);
  set_timer("run_away", 5);
  set_timer("OWG", 10);

  num_bases_left = num_type_upgrade(2, "Barracks") + num_type_upgrade(2, "Tower");

  ctw_add_objective_text(parse("There are $d0 guerrilla bases remaining.", num_bases_left), "RunningTotal", 0);
}

if (num_bases_left > num_type_upgrade(2, "Barracks") + num_type_upgrade(2, "Tower")) {
  num_bases_left = num_type_upgrade(2, "Barracks") + num_type_upgrade(2, "Tower");
  if (num_bases_left == 1) {
    ctw_change_objective_text("RunningTotal", parse("There are $d0 guerrilla base remaining.", num_bases_left));
  }
  else {
    ctw_change_objective_text("RunningTotal", parse("There are $d0 guerrilla bases remaining.", num_bases_left));
  }
}

//keep the generals in ambush mode
if (timer_expired("GA")) {
  for (n = num_type_upgrade(2, "General"); n > 0; n--) {
    unit_id = find_unit(2, "General");
    if (!general_is_ambushing(2, unit_id)) {
      if (has_move_order(2, unit_id)) {
        general_ambush(2, unit_id);
        general_id = unit_id;
        enable_trigger("KeepMoving");
        set_timer("KM", 1);
      }
      else {
        general_ambush(2, unit_id);
      }
    }
  }
  set_timer("GA", 1);
}

trigger KeepMoving(timer_expired("KM"))
{
  city_name = find_city_name(1);
  city_id = find_city_id(city_name);

  x = object_position_x(1, city_id);
  y = object_position_y(1, city_id);

  unit_move_order(2, general_id, x, y);
}

////conduct a raid/////////////
trigger OffWeGo(timer_expired("OWG"))
{
  unit_id = -1;

  for (i = 0; i < num_type_upgrade(2, "General"); i++) {
    unit_id = find_unit(2, "General");
    if (!has_move_order(2, unit_id)) {
      x = rand_int(5, get_map_size()-5);
      y = rand_int(5, get_map_size()-5);
      unit_move_order(2, unit_id, x, y);
    }
  }
  enable_trigger("OffWeGo");
  set_timer("OWG", 60);
}

///if the player can see the general than the general runs away
if (timer_expired("run_away")) {
  for (n = num_type_upgrade(2, "General"); n > 0; n--) {
    x = rand_int(5, get_map_size()-5);
    y = rand_int(5, get_map_size()-5);

    unit_id = find_unit(2, "General");
    if (object_visible(1, 2, unit_id)) {
      unit_move_order(2, unit_id, x, y);
    }
  }
  set_timer("run_away", 5);
}

//spawn new peruvian guerillas
trigger Peruvian_Guerillas(timer_expired("PG"))
{
  victim = rand_int(1, num_cities(1)+1);
  city_id = -1;
  city_name = "";
  
  for (i = 0; i < num_type_upgrade(2, "Barracks"); i++) {
    if (num_type_upgrade(2, "Royal Tercios") > diff*3) {
      break;
    }
    build_id = find_build(2, "Barracks");
    x = object_position_x(2, build_id);
    y = object_position_y(2, build_id);
    clear_group(2);
    create_unit_in_group(2, x + 1, y, "Infantry", 1);
    create_unit_in_group(2, x + 1, y, "Bazooka", 1);
    group_stance_order(2, "Raid");

    for (j = 0; j < victim; j++) {
      city_name = find_city_name(1);
    }
    city_id = find_city_id(city_name);
    x = object_position_x(1, city_id);
    y = object_position_y(1, city_id);
    group_attack_to_order(2, x, y);
  }
  
  enable_trigger("Peruvian_Guerillas");
  set_timer("PG", 720/diff);
}

if (num_buildings(2) < 1) {
  victory(1);
}

if (time() >= get_time_limit() && num_buildings(2) > 0) {
  defeat(1);
}

}
