             conquest
{
int x = 0;
int y = 0;
int general_id = 0;
static int diff = get_difficulty();
static string capital_name;

run_once {
  set_timer("GA", 2);
  if (diff > 3) {
    set_timer("PG", 180);
  }
  set_timer("run_away", 5);
  set_timer("OWG", 10);
  capital_name = find_capital(2);
  disable_trigger("BritishInvolvement");
}

//keep the generals in ambush mode
if (timer_expired("GA")) {
  for (n = num_type_upgrade(2, "General"); n > 0; n--) {
    unit_id = find_unit(2, "General");
    if (!general_is_ambushing(2, unit_id)) {
      if (has_move_order(2, unit_id)) {
        general_ambush(2, unit_id);
        general_id = unit_id;
        enable_trigger("KeepMoving");
        set_timer("KM", 1);
      }
      else {
        general_ambush(2, unit_id);
      }
    }
  }
  set_timer("GA", 1);
}

trigger KeepMoving(timer_expired("KM"))
{
  city_name = find_city_name(1);
  city_id = find_city_id(city_name);

  x = object_position_x(1, city_id);
  y = object_position_y(1, city_id);

  unit_move_order(2, general_id, x, y);
}

////conduct a raid/////////////
trigger OffWeGo(timer_expired("OWG"))
{
  unit_id = -1;

  for (i = 0; i < num_type_upgrade(2, "General"); i++) {
    unit_id = find_unit(2, "General");
    if (!has_move_order(2, unit_id)) {
      x = rand_int(5, get_map_size()-5);
      y = rand_int(5, get_map_size()-5);
      unit_move_order(2, unit_id, x, y);
    }
  }
  enable_trigger("OffWeGo");
  set_timer("OWG", 60);
}

///if the player can see the general than the general runs away
if (timer_expired("run_away")) {
  for (n = num_type_upgrade(2, "General"); n > 0; n--) {
    x = rand_int(5, get_map_size()-5);
    y = rand_int(5, get_map_size()-5);

    unit_id = find_unit(2, "General");
    if (object_visible(1, 2, unit_id)) {
      unit_move_order(2, unit_id, x, y);
    }
  }
  set_timer("run_away", 5);
}

//spawn new spanish guerillas
trigger Portugese_Guerillas(timer_expired("PG"))
{
  victim = rand_int(1, num_cities(1)+1);
  city_id = -1;
  city_name = "";
  
  for (i = 0; i < num_type_upgrade(2, "Barracks"); i++) {
    if (num_type_upgrade(2, "Fusiliers") > diff*4) {
      break;
    }
    build_id = find_build(2, "Barracks");
    x = object_position_x(2, build_id);
    y = object_position_y(2, build_id);
    clear_group(2);
    if (ctw_is_ally("British", "Spanish")) {
      create_unit_in_group(2, x + 1, y, "Highlanders", 1);
    }
    else {
      create_unit_in_group(2, x + 1, y, "Musketeers", 1);
    }
    create_unit_in_group(2, x + 1, y, "Fusiliers", 1);
    group_stance_order(2, "Raid");

    for (j = 0; j < victim; j++) {
      city_name = find_city_name(1);
    }
    city_id = find_city_id(city_name);
    x = object_position_x(1, city_id);
    y = object_position_y(1, city_id);
    group_attack_to_order(2, x, y);
  }
  
  enable_trigger("Portugese_Guerillas");
  set_timer("PG", 720/diff);
}

trigger CapitalLost(city_captured(2, capital_name) && timer_expired("CL") != 0)
{
  city_id = find_city_id(capital_name);
  x = object_position_x(1, city_id);
  y = object_position_y(1, city_id);

  for (i = 0; i < num_type_upgrade(2, "General"); i++) {
    unit_id = find_unit(2, "General");
    unit_move_order(2, unit_id, x, y);
  }

  set_timer("CL", 5);
  enable_trigger("CapitalLost");
}

trigger BritishInvolvement(city_captured(2, capital_name) || num_cities(2) < 2)
{
  choice1 = popup_choice($S("An emissary of Great Britain approaches: We wish to speak with you on behalf of our Portugese friends."), $S("Listen"), $S("Don't listen"));
  if (choice1 == 1) {
    if (ctw_is_war("British", "French")) {
      choice2 = popup_choice($S("We see that you have placed the Portuguese throne in dire straits. We wish to settle this affair without any further bloodshed. If you agree to sign a peace treaty with Portugal and leave this province then we will also sign a peace treaty with you."), $S("Sign peace treaty"), $S("Don't sign"));
      if (choice2 == 1) {
        ctw_set_peace("French", "Inca");
        ctw_set_peace("French", "British");
        popup_dialog($S("Peace signed with Portugal."));
        popup_dialog($S("Peace signed with Great Britain."));
        set_ctw_int("PortMadPeace", 1);
        defeat(1);
      }
    }
    else {
      choice2 = popup_choice($S("We see that you have placed the Portuguese throne in dire straits. We wish to settle this affair without any further bloodshed, but if you do not agree to sign a peace treaty with Portugal and leave this province immediately we will be forced to take action!"), $S("Sign peace treaty"), $S("Don't sign"));
      if (choice2 == 1) {
        ctw_set_peace("French", "Inca");
        popup_dialog($S("Peace signed with Portugal."));
        defeat(1);
      }
      else {
        ctw_set_war("French", "British");
        popup_dialog($S("Great Britain has declared war upon you!"));
      }
    }
  }
}

if(time() >= get_time_limit()) {
  defeat(1);
}

}
