 scenario
{

labels {
  //Add any constant labels here in a comma separated list.
}

static napoleon_id = -1;
static diff = get_difficulty();

static int end_time = get_time_limit();
int cur_time;

run_once {
  disable_all_triggers();
  
  napoleon_id = find_unit(1, "Napoleon");

  ctw_add_objective_text($S("Capture the enemy's capital."), "Hint2_id", 0);
  ctw_add_objective($S("Hint: Capture Vannes"), "Hint1_id", 0);

  enable_trigger("MoreNavy");
  enable_trigger("VanneComplete");
  enable_trigger("BritainInit");
  if (get_ctw_int("emperor") > 0) {
    enable_trigger("PeaceOffer");
  }
}

cur_time = time_min();
if (cur_time >= end_time) {
  defeat(1);
}

trigger BritainInit()
{
  group_patrol_order(2, 30, 78);
}

trigger VanneComplete(city_captured(2, "Vannes"))
{
  ctw_objective_complete("Hint1_id");
}

trigger MoreNavy(num_type(2, "Man o' War")+num_type(2, "Sloop")+num_type(2, "Bomb Ketch") < diff*3)
{
  clear_group(2);
  x = 230;
  y = 6;
  create_unit_in_group(2, x, y, "Man o' War", diff/2+1);
  create_unit_in_group(2, x, y, "Sloop", diff/2+1);
  create_unit_in_group(2, x, y, "Bomb Ketch",diff/2+2); 
  group_patrol_order(2, 30, 78);
}

trigger PeaceOffer(city_captured(2, "Saint-Brieuc") || city_captured(2, "Quimper"))
{
  choice1 = -1;
  choice2 = -1;
  choice1 = popup_choice($S("An emissary of the British has asked for an audience."), $S("Listen to him"), $S("Don't listen"));

  if (choice1 == 1) {
    choice2 = popup_choice(parse("We wish to put an end to the hostilities between our great nations. We will agree to leave this province and sign a peace treaty for 20 tribute. (You have $d0 Tribute.)", get_num_tribute("French")), $S("Pay 20"), $S("Refuse offer"));
    if (choice2 == 1) {
      if (get_num_tribute("French") < 20) {
        popup_dialog($S("Unfortunately you do not have 20 Tribute."));
      }
      else {
        give_ctw_tribute("french", -20);
        defeat(2);
        ctw_set_peace("French", "British");
      }
    }
    else {
      popup_dialog($S("Very well, but remember this: Any further loss of life is on your head!"));
    }
  }
}

}