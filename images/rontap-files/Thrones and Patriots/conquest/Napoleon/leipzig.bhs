scenario
{

labels {
  HUMAN = 1,
  PRUSSIAN = 2,
  AUSTRIAN = 3,
  RUSSIAN = 4,
  SWEEDISH = 5
}

  int building;
  int univ2_int;
  int univ3_int;
  int univ4_int;
  int univ5_int;
  int choice_int;
  static int diff_int = get_difficulty();
  static int diffmod_int;
  static int prussiancap_int = 50 + 5*diff_int;
  static int othercap_int = 20 + 2*diff_int;
  static int end_time = get_time_limit();
  int cur_time;


run_once {
  switch (diff_int) {
    case 1:
    case 2:
      diffmod_int = 1;
      break;
    case 3:
    case 4:
      diffmod_int = 2;
      break;
    case 5:
    case 6:
      diffmod_int = 3;
      break;
  } // switch (diff_int)

  set_population_cap(PRUSSIAN, prussiancap_int);
  set_population_cap(SWEEDISH, othercap_int);
  set_population_cap(AUSTRIAN, othercap_int);
  set_population_cap(RUSSIAN, othercap_int);
  disable_city_defeat(HUMAN);
  disable_city_defeat(SWEEDISH);
  disable_city_defeat(AUSTRIAN);
  disable_city_defeat(RUSSIAN);
  disable_type("small city");
  disable_type("farm");
  disable_type("woodcutter's camp");
  disable_type("mine");
  disable_type("university");
  disable_type("granary");
  disable_type("lumber mill");
  disable_type("smelter");
  disable_type("senate");
  set_nation_name("Romans", "Sweeds");
  set_nation_name("Greeks", "Austrians");
  ctw_add_objective($S("Get a foothold in the region at Lutzen."), "lutzen_obj", "");

  set_base_rate(SWEEDISH, "metal", 10);
  set_base_rate(SWEEDISH, "wealth", 10);
  set_base_rate(RUSSIAN, "metal", 10);
  set_base_rate(RUSSIAN, "wealth", 10);
  set_base_rate(AUSTRIAN, "timber", 10);
  set_base_rate(AUSTRIAN, "wealth", 10);
  set_base_rate(PRUSSIAN, "knowledge", 50);
  
  create_unit_upgrade(PRUSSIAN, 5, 136, "supply wagon", 1);
  create_unit_upgrade(PRUSSIAN, 5, 136, "cannon", 2);
  create_unit_upgrade(PRUSSIAN, 5, 136, "musketeer", diffmod_int + 1);
  create_unit_upgrade(PRUSSIAN, 5, 136, "fusiliers", diffmod_int + 1);
}

cur_time = time_min();
if (cur_time >= end_time) {
  defeat(1);
}

trigger (num_cities(1) > 0) {
  city_name = find_city_name(1);
  set_capital(1, city_name);
}

if (population_cap(PRUSSIAN) > prussiancap_int) set_population_cap(PRUSSIAN, prussiancap_int);
if (population_cap(SWEEDISH) > othercap_int) set_population_cap(SWEEDISH, othercap_int);
if (population_cap(AUSTRIAN) > othercap_int) set_population_cap(AUSTRIAN, othercap_int);
if (population_cap(RUSSIAN) > othercap_int) set_population_cap(RUSSIAN, othercap_int);

// Destroy inactive universities
univ2_int = find_inactive_build(SWEEDISH, "University");
univ3_int = find_inactive_build(AUSTRIAN, "University");
univ4_int = find_inactive_build(RUSSIAN, "University");
univ5_int = find_inactive_build(PRUSSIAN, "University");

trigger univkill2_trg(univ2_int > -1) {
  destroy_building(SWEEDISH, univ2_int);
  enable_trigger ("univkill2_trg");
} // trigger univkill2_trg
trigger univkill3_trg(univ3_int > -1) {
  destroy_building(AUSTRIAN, univ3_int);
  enable_trigger ("univkill3_trg");
} // trigger univkill3_trg
trigger univkill4_trg(univ4_int > -1) {
  destroy_building(RUSSIAN, univ4_int);
  enable_trigger ("univkill4_trg");
} // trigger univkill4_trg
trigger univkill5_trg(univ5_int > -1) {
  destroy_building(PRUSSIAN, univ5_int);
  enable_trigger ("univkill5_trg");
} // trigger univkill5_trg

// **Lutzen taken
trigger lutzen_trg(city_captured_by(PRUSSIAN, HUMAN, "Lutzen")) {
  ctw_objective_complete("lutzen_obj");
  city_assimilate(HUMAN, "Lutzen");
  popup_dialog($S("Excellent work! The citizens of the area will not succumb to your rule easily. If you seized the area's economy and infrastructure, they would have no choice but to bow to your power. Find and control the major centers for Food, Timber, Metal, Knowledge and Government."));
  ctw_add_objective($S("Capture a Granary to help your Food production."), "granary_obj", "");
  ctw_add_objective($S("Capture a Lumber Mill to help your Timber production."), "lumber_obj", "");
  ctw_add_objective($S("Capture a Smelter to help your Metal production."), "smelter_obj", "");
  ctw_add_objective($S("Capture a University to help your Knowledge production."), "university_obj", "");
  ctw_add_objective($S("Capture a Senate in order to use Governments."), "senate_obj", "");
  set_timer("schwarzenberg_tmr", 150 - (10*diff_int));
} // trigger lutzen_trg

// **Dennewitz taken, granary enabled
trigger granary_trg(city_captured_by(PRUSSIAN, HUMAN, "Dennewitz")) {
  ctw_objective_complete("granary_obj");
  popup_dialog($S("Well done! You now control the majority of food production in the region and can comfortably expand your farmer employment."));
  enable_type("farm");
  enable_type("granary");
  set_timer("bernadotte_tmr", 20);
} // trigger granary_trg

// **Gross Beeren taken, lumbermill enabled
trigger lumber_trg(city_captured_by(SWEEDISH, HUMAN, "Gross Beeren")) {
  ctw_objective_complete("lumber_obj");
  popup_dialog($S("Great job! You now control the majority of wood production in the region and can comfortably expand your woodcutter employment."));
  enable_type("woodcutter's camp");
  enable_type("lumber mill");
} // trigger lumber_trg

// **Dresden taken, university enabled
trigger university_trg(city_captured_by(AUSTRIAN, HUMAN, "Dresden")) {
  ctw_objective_complete("university_obj");
  popup_dialog($S("Superb! You now control the enlightened population in the region and can expand the schooling as you see fit."));
  enable_type("university");
} // trigger university_trg

// **Bautzen taken, smelter enabled
trigger smelter_trg(city_captured_by(PRUSSIAN, HUMAN, "Bautzen")) {
  ctw_objective_complete("smelter_obj");
  popup_dialog($S("Magnificent! You now control the majority of metal production in the region and can comfortably expand your miner employment."));
  enable_type("mine");
  enable_type("smelter");
} // trigger smelter_trg

// **Liegnutz taken, senate enabled
trigger senate_trg(city_captured_by(PRUSSIAN, HUMAN, "Liegnutz")) {
  ctw_objective_complete("senate_obj");
  if (find_city_owner("Dennewitz") != 1) {
    building = find_build_at_city(HUMAN, "Liegnutz", "granary", 1);
    if (building > -1) destroy_building(HUMAN, building);
  }
  if (find_city_owner("Gross Beeren") != 1) {
    building = find_build_at_city(HUMAN, "Liegnutz", "lumber mill", 1);
    if (building > -1) destroy_building(HUMAN, building);
  }
  if (find_city_owner("Dresden") != 1) {
    building = find_build_at_city(HUMAN, "Liegnutz", "university", 1);
    if (building > -1) destroy_building(HUMAN, building);
  }
  popup_dialog($S("Fantastic! You now control the voice of the people in this region and can manipulate the law as you see fit from a Senate."));
  enable_type("Senate");
} // trigger senate_trg

// **Schwarzenberg arrives
trigger schwarzenberg_trg(timer_expired("schwarzenberg_tmr")) {
  popup_dialog($S("Our scouts have reported that Commander Schwarzenberg is arriving with an army from the south!"));
  add_reveal_point(HUMAN, 173, 192, 15);
  create_unit_upgrade(AUSTRIAN, 173, 192, "commander schwarzenberg", 1);
  create_unit_upgrade(AUSTRIAN, 173, 192, "commando", diffmod_int + 1);
  create_unit_upgrade(AUSTRIAN, 173, 192, "supply wagon", 1);
  create_unit_upgrade(AUSTRIAN, 173, 192, "cannon", 3);
  create_unit_upgrade(AUSTRIAN, 173, 192, "musketeer", diffmod_int);
  create_unit_upgrade(AUSTRIAN, 173, 192, "cataphract", diffmod_int + 2);
  enable_city_defeat(AUSTRIAN);
  set_timer("pingaustrian_tmr", 4);
  set_timer("clearreveal_tmr", 10);
  set_timer("bernadotte_tmr", 300 - (10*diff_int));
} // trigger scwarzenberg_trg

trigger pingaustrian_trg(timer_expired("pingaustrian_tmr")) {
  ping(HUMAN, HUMAN, 173, 192);
} // trigger pingaustrian_trg

trigger clearreveal_trg(timer_expired("clearreveal_tmr")) {
  clear_reveal_points(HUMAN);
  enable_trigger ("clearreveal_trg");
} // trigger clearreveal_trg

// **Bernadotte and Bennigsen arrive
trigger bernadotte_trg(timer_expired("bernadotte_tmr")) {
  choice_int = popup_choice($S("The cunning Archduke Charles and General Blucher have been spotted to the north with reinforcements! We are badly outnumbered. If we retreat now, we may be able to regroup with the spoils already taken from the area. (Gain 100 Tribute and a Mercenaries Bonus Card.)"), $S("Retreat!"), $S("Fight!"));
  if (choice_int == 1) {
    give_player_ctw_tribute(100);
    give_player_bonus_card("Mercenaries");
    set_ctw_int("leip_retreat", 1);
    defeat(HUMAN);
  } // if (choice_int
  else {
    add_reveal_point(HUMAN, 24, 3, 15);
    add_reveal_point(HUMAN, 122, 4, 15);
    create_unit_upgrade(SWEEDISH, 24, 3, "archduke charles", 1);
    create_unit_upgrade(SWEEDISH, 24, 3, "general blucher", 1);
    create_unit_upgrade(SWEEDISH, 24, 3, "supply wagon", 1);
    create_unit_upgrade(SWEEDISH, 24, 3, "cannon", 3);
    create_unit_upgrade(SWEEDISH, 24, 3, "musketeer", diffmod_int);
    create_unit_upgrade(SWEEDISH, 24, 3, "fusiliers", diffmod_int + 2);
    set_timer("pingsweed_tmr", 4);
    set_timer("clearreveal_tmr", 14);
    enable_city_defeat(SWEEDISH);
    enable_city_defeat(RUSSIAN);
  } // else
} // trigger bernadotte_trg

trigger pingsweed_trg(timer_expired("pingsweed_tmr")) {
  ping(HUMAN, HUMAN, 24, 3);
  create_unit_upgrade(RUSSIAN, 122, 4, "General kutosov", 1);
  create_unit_upgrade(RUSSIAN, 122, 4, "supply wagon", 1);
  create_unit_upgrade(RUSSIAN, 122, 4, "cannon", 3);
  create_unit_upgrade(RUSSIAN, 122, 4, "light horse", diffmod_int + 2);
  create_unit_upgrade(RUSSIAN, 122, 4, "fusiliers", diffmod_int);
  set_timer("pingrussian_tmr", 4);
} // trigger pingsweed_trg

trigger pingrussian_trg(timer_expired("pingrussian_tmr")) {
  ping(HUMAN, HUMAN, 122, 4);
} // trigger pingrussian_trg
}