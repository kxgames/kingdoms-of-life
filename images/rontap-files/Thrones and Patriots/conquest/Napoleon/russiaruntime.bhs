 scenario
{

labels {
  //Add any constant labels here in a comma separated list.
}

static message_flag = 0;
static borodino_id = 0;
static napoleon_id = 0;
static diff = get_difficulty();
static int timer_counter = 0;
static int city_counter = 0;
static int message_counter = 1;

run_once {
  disable_all_triggers();
  show_all_map_enable(2);
  disable_city_ai(2);
  disable_city_defeat(1);
  //disable_city_defeat(2);

  napoleon_id = find_unit(1, "Napoleon");
  borodino_id = find_city_id("Borodino");

  ctw_add_objective_text($S("Capture the enemy's capital."), "Hint_id", 0);
  ctw_add_objective($S("Hint: Capture Smolensk."), "One_id", 0);
  city_id = find_city_id("Smolensk");
  set_seen(1, 2, city_id);
  enable_trigger("CossackRaid");
  enable_trigger("CityRazeSetup");
  enable_trigger("BorodinoSpawn");
  enable_trigger("MoscowLost");
  enable_trigger("SmolenskLost");
  set_timer("CR", 30);
  timer_counter = 460/diff;
}

if (population_cap(2) > 90) {
  set_population_cap(2, 90);
}

//Don Cossack!!!
trigger CossackRaid(timer_expired("CR"))
{
  clear_group(2);
  for(i = 0; i < num_type_upgrade(2, "Fortress"); i++) {
    build_id = find_build(2, "Fortress");
    if (build_id > -1) {
      x = object_position_x(2, build_id);
      y = object_position_y(2, build_id);
      if (diff < 5) {
        create_unit_in_group(2, x, y, "Don Cossack", 1);
      }
      else {
        create_unit_in_group(2, x, y, "Don Cossack", 2);
      }
    }
    unit_id = find_unit(1, "Supply Wagon");
    if (unit_id > -1) {
      group_attack_order(2, 1, unit_id);
    }
    else {
      group_attack_order(2, 1, napoleon_id);
    }
  }
  if (timer_counter == 430/diff) {
    //clear_group(2);
    for(i = 0; i < num_type_upgrade(2, "Barracks"); i++) {
      build_id = find_build(2, "Barracks");
      if (build_id > -1) {
        x = object_position_x(2, build_id);
        y = object_position_y(2, build_id);
        if (diff < 5) {
          create_unit_in_group(2, x, y, "Musketeers", 1);
        }
        else {
          create_unit_in_group(2, x, y, "Musketeers", 1);
          create_unit_in_group(2, x, y, "Fusiliers", 1);
          create_unit_in_group(2, x, y, "Supply Wagon", 1);
        }
      }
      city_id = find_city_id("Moscow");
      x = object_position_x(1, city_id);
      y = object_position_y(1, city_id);
      group_attack_to_order(2, x, y);
    }
  }
  if (diff > 2) {
    enable_trigger("CossackRaid");
    set_timer("CR", timer_counter);
  }
}

trigger BorodinoSpawn(any_object_near_build(1, 1, 2, borodino_id, 25))
{
  x = object_position_x(2, borodino_id);
  y = object_position_y(2, borodino_id);

  clear_group(2);
  create_unit_in_group(2, x, y, "Musketeers", diff-2);
  create_unit_in_group(2, x, y, "Fusiliers", diff-3);
  create_unit_in_group(2, x, y, "Cuirassier", diff/2);
  create_unit_in_group(2, x, y, "Carabineer", diff/2);
  create_unit_in_group(2, x, y, "Cannon", diff-2);
  create_unit_in_group(2, x, y, "Commando", 1);
  create_unit_in_group(2, x, y, "General Kutosov", 1);

  x = object_position_x(2, napoleon_id);
  y = object_position_y(2, napoleon_id);
  group_attack_to_order(2, x, y);
}

trigger CityRazeSetup(num_cities(1) > 0)
{
  set_timer("CRS", 5);
  enable_trigger("CityRaze");
}

trigger CityRaze(timer_expired("CRS"))
{
  farm_id = -1;
  for (i = 1; i <= num_cities(1); i++) {
    city_name = find_city_with_num(1, i);
    if (city_name != "Moscow") {
      do {
        farm_id = find_build_at_city(1, city_name, "Farm", 1);
        destroy_building(1, farm_id);
      } while(farm_id > -1);

      city_id = find_city_id(city_name);

      build_id = find_build_at_city(1, city_name, "", 1);
      start_build_id = build_id;
      do {
        set_object_health(1, build_id, 5);
        build_id = find_build_at_city(1, city_name, "", 1);
      }while(build_id != start_build_id);
    
      bubble_text_obj(parse("Russian partisans have set fire to $s0! The city center has been razed to the ground!", city_name), 1, city_id);
      message_flag--;
      destroy_building(1, city_id);
    }
    else {
      city_counter = 1;
      message_counter = 2;
    }
  }
  
  enable_trigger("CityRazeSetup");
}

trigger SmolenskLost(city_captured(2, "Smolensk"))
{
  ctw_objective_complete("One_id");
  ctw_add_objective($S("Hint: Capture Borodino."), "Two_id", 0);
  set_seen(1, 2, borodino_id);
  enable_trigger("BorodinoLost");
}

trigger BorodinoLost(city_captured(2, "Borodino"))
{
  ctw_objective_complete("Two_id");
}

trigger MoscowLost(city_captured(2, "Moscow"))
{
  if (diff < 3) {
    victory(1);
  }
  else {
    popup_dialog($S("The Russians have declared that they will continue fighting until the bitter end! Hold Moscow until the timer runs out to defeat them!"));

    ctw_remove_objective("Hint_id");
    ctw_add_objective_text($S("Hold Moscow until the timer runs out."), "FinalHint_id", 0);
    timer_counter = 430/diff;

    set_base_rate(1, "Food", 200);
    set_base_rate(1, "Timber", 200);
    set_base_rate(1, "Metal", 200);
    set_base_rate(1, "Wealth", 200);
  }
}

if (num_cities(1) > city_counter && message_flag < message_counter) {
  message_flag = num_cities(1);
  city_name = find_city_name(1);
  skipcheck = 0;
  if (city_name == "Moscow" && diff < 3) {
    skipcheck = 1;
  }
  else {
    skipcheck = 0;
  }
  if (get_time_limit() - time() > 1 && skipcheck < 1) {
    choice = popup_choice(parse("Taking $s0 has increased the security of your supply lines. You can buy 5 supply wagons for 5 tribute or 5 cannons for 5 tribute at this time.(You have $d1)", city_name, get_num_tribute("French")), $S("Wagons"), $S("Cannons"), $S("Don't buy"));
    if (choice == 0) { //yes
      if (get_num_tribute("French") >= 5) {
        x = object_position_x(1, napoleon_id);
        y = object_position_y(1, napoleon_id);
        create_unit_upgrade(1, x, y, "Supply Wagon", 5);
        give_ctw_tribute("French", -5);
      }
      else {
        popup_dialog($S("Regrettably, you do not have enough tribute to buy the supply wagons."));
      }
    }
    else if (choice == 1) { //yes
      if (get_num_tribute("French") >= 5) {
        x = object_position_x(1, napoleon_id);
        y = object_position_y(1, napoleon_id);
        create_unit_upgrade(1, x, y, "Cannon", 5);
        give_ctw_tribute("French", -5);
      }
      else {
        popup_dialog($S("Regrettably, you do not have enough tribute to buy any more cannons."));
      }
    }
  }
}

if (time() >= get_time_limit()) {
  if (find_city_owner("Moscow") == 1) {
    victory(1);
  }
  else {
    defeat(1);
  }
}

if (find_military(1) < 0 && num_cities(1) < 1) {
  defeat(1);
}

if (city_captured(2, "Moscow")) {
  city_assimilate(1, "Moscow");
}

}