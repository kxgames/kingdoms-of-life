//Pillars of Hercules Map Script
map make(int num_players)
{
  states {
    DIST_MIN_COAST = 1
  }


  clear_map();

  world_size = get_world_size();
  land_goal = get_num_tiles() * 0.40;
  radius = world_size / 3;

  x = (world_size / 2) + rand_int(0, 2);
  y = (world_size / 2) + rand_int(0, 2);
  
  make_region (1, x, y, land_goal);

  if (grow_region (1, land_goal, radius)) {
    debug_print ("Failed to grow main region");
    return 1;
  }
  
  invert_land();
  
  edge = false;
  arc  = 360.0 / num_game_nations();
  angle_in_degrees = rand_int(0, arc / 16) * 16;

  int wx, wy;
    
  for (who = 0; who < num_players; who++) {

    angle_in_degrees += arc;

    dist = world_size * 3/2;

    do {
      project_to_plane(x, y, angle_in_degrees, dist, wx, wy);
      dist -= 3;

      edge = false; 
      for (scan = 0; scan < NUM_CORNERS; scan++) {
        xx = wx + get_orthog_x(scan);
        yy = wy + get_orthog_y(scan);
        
        if (!tile_is_valid(xx, yy) || tile_is_edge(xx, yy)) {
          edge = true;
          break;
        }
      }      
    } while (!tile_is_valid(wx, wy) || tile_is_ocean(wx, wy) || edge);

    add_starting_location(wx, wy);
  }
  
  check_player_land(true, DIST_MIN_COAST);
}
